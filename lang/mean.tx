/*
  This is a textX specification of MEAN.TX DSL for generating MEAN.IO stack applications.
  Authors: Nemanja Ristić <twcbason AT hotmail DOT com>
           Daniel Kupčo <kupcodanex AT outlook DOT com>
           Novak Boškov <gnovak DOT boskov AT gmail DOT com>
*/

MeanModel:
  "app" name=STRING
  (description=STRING)?
  blocks+=BlockType
;

BlockType:
   PartType | ActionType | ActorType | ActorGroupType
;

PartType:
  'part' '{'
    // TODO: Every piece must be mentioned once but order doesn't matter
    namePiece=PartNamePiece
    multiPiece=PartMultiPiece
    propertiesPiece=PartPropertiesPiece
    actionPiece=PartActionPiece
    ownerPart=PartOwnerPiece
  '}'
;

PartNamePiece:
  'name:' partname=STRING
;

PartMultiPiece:
  'multi:' multi=YesNo
;

YesNo:
  'yes' | 'no'
;

PartPropertiesPiece:
  'properties:' '{'
     properties*=Property[',']
   '}'
;

Property:
  '[' name=STRING ',' type=Type (',' visibility=Visible)?']'
;

Visible:
  'visible'
;

Type:
  'String' | 'Text' | 'Number' | 'Boolean' | Reference
;

Reference:
  '~'name=STRING
;
  
PartActionPiece:
  'actions:' '{'
    actions*=Reference[',']
  '}'
;

PartOwnerPiece:
  'owner:' '{'
    owners*=Reference[',']
  '}'
;

ActionType:
  '->action' '{'
    ActionNamePiece
    ActionDoPiece
    ActionOnPiece
    ActionWhoPiece
  '}'
;

ActionNamePiece:
  'name:' name=STRING
;

ActionDoPiece:
  'do:' actionCRUD=CRUDOperation (ActionDoPiecePrompt)?
;

CRUDOperation:
  'create' | 'read' | 'update' | 'delete'
;

ActionDoPiecePrompt:
  '[' 'prompt:' prompt=YesNo ']'
;

ActionOnPiece:
  'on:' (ActionOnPiecePart | ActionOnPieceParts)
;

ActionOnPiecePart:
  part=Reference
;

ActionOnPieceParts:
  '['
    parts+=ActionOnPiecePart[',']
  ']'
;

ActionWhoPiece:
  'who:' (ActionWhoPieceOwner | WhoItems | Anyone)
;

Anyone:
  '*'
;

ActionWhoPieceOwner:
  // TODO: 'owner' is owner of referenced part owner if exists
  // whoItems explain which actor or which actors have permission to do the action
  'owner' (WhoItems)?
;

WhoItems:
  '['
    whoItems*=WhoItem[',']
  ']'
;

WhoItem:
  who=Reference
;

ActorType:
  'actor' '{'
    ActorNamePiece
    ActorGroupPiece
  '}'
;

ActorNamePiece:
  'name:' name=STRING
;

ActorGroupPiece:
  'group:' (ActorGroupPieceGroup | ActorGroupPieceGroups)
;

ActorGroupPieceGroups:
  '['
    groups+=ActorGroupPieceGroup[',']
  ']'
;

ActorGroupPieceGroup:
  actor_group=Reference
;

ActorGroupType:
  'actor-group' '{'
    ActorGroupNamePiece
    ActorGroupActionsPiece
  '}'
;

ActorGroupNamePiece:
  'name:' name=STRING
;

ActorGroupActionsPiece:
  'actions:' (ActorGroupActionsPieceAction | ActorGroupActionsPieceActions)
;

ActorGroupActionsPieceActions:
  '['
    actions+=ActorGroupActionsPieceAction[',']
  ']'
;

ActorGroupActionsPieceAction:
  action=Reference
;
